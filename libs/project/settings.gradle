pluginManagement {
    repositories {
        mavenCentral()
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        maven { url 'https://jitpack.io' }
        google()  // 阿里云对于有些库会有丢失，加入进行兜底
        mavenCentral()
        gradlePluginPortal()
    }
}


dependencyResolutionManagement {
    /**
     * FAIL_ON_PROJECT_REPOS:  如果设置此模式，则在项目中直接或通过插件直接声明的任何存储库都将触发构建错误
     * PREFER_PROJECT:         如果设置此模式，则在项目上声明的任何存储库会覆盖设置中声明的存储库
     * PREFER_SETTINGS：       如果设置此模式，则项目中使用的存储库只会以设置中声明的为准
     */
    repositoriesMode.set(RepositoriesMode.PREFER_PROJECT)

    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public' } // central 和 jcenter 的聚合仓
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://jitpack.io' }
        google()  // 阿里云对于有些库会有丢失，加入进行兜底
        mavenCentral()

        maven {
            name = "GitHubPackages"
            url 'https://maven.pkg.github.com/Heart-Beats/BaseProject'
            credentials {
                username = 'Heart-Beats'
                password = GITHUB_PACKAGES_READ_TOKEN
            }
        }
    }

    versionCatalogs {
        // 默认创建的 libs 进行配置
        libs {
            from("com.hl:catalog:1.0.0-SNAPSHOT")

            version("androidGradlePlugin", "8.0.1")
            version("kotlin", "1.8.20")
        }
    }
}

rootProject.name='Project'

// 不添加 includeBuild， 该项目下无法依赖 uikit 项目下的相关库
includeBuild('../uikit')

includeBase()
includeSDK()
include ':utils'

def includeBase() {
    include ':base-api', ':base-app-res', ':base-ui', ':base-arch'

    project(':base-api').projectDir = file('./base/api')
    project(':base-app-res').projectDir = file('./base/app-res')
    project(':base-ui').projectDir = file('./base/ui')
    project(':base-arch').projectDir = file('./base/arch')
}

def includeSDK() {
    include ':SDK:Umeng', ':SDK:Pay', ':SDK:TencentCloud', 'SDK:Navigation', ':SDK:Web', ':SDK:ActivityResult',
            ':SDK:XLogInit', ':SDK:MMKVSharedPreferences', ':SDK:JsonUtil', ':SDK:Permission', ':SDK:PictureSelector',
            ':SDK:ImageLoad', ':SDK:DateUtil', ':SDK:BitmapUtil', ':SDK:MimeType', ':SDK:Download', ':SDK:QRCode',
            ':SDK:Popup', ':SDK:SmsUtil', ':SDK:PreviewFile', ':SDK:Share', ':SDK:VideoPlayer', ':SDK:Camera'

    includeShadow()

    includeUniApp()
}

def includeShadow() {
    //    include ':SDK:shadow-plugin-manager'
    include ':SDK:shadow-lib', ':SDK:shadow-init'

    //    project(':SDK:shadow-plugin-manager').projectDir = file('./SDK/Shadow/plugin-manager')
    project(':SDK:shadow-lib').projectDir = file('./SDK/Shadow/shadow-lib')
    project(':SDK:shadow-init').projectDir = file('./SDK/Shadow/shadow-init')
}

def includeUniApp() {
    include ':SDK:UniMP'

    // 加入 UniApp 需要依赖的本地 AAR
    includeUniAppAARS()
}

def includeUniAppAARS() {
    include ':SDK:uniapp-base-oaid-sdk', ':SDK:uniapp-breakpad-build-release', ':SDK:uniapp-v8-release', ':SDK:uniMPSDK-V2-release'

    project(':SDK:uniapp-base-oaid-sdk').projectDir = file('./SDK/LocalAAR/uniapp-aars/base-oaid-sdk-aar')
    project(':SDK:uniapp-breakpad-build-release').projectDir = file('./SDK/LocalAAR/uniapp-aars/breakpad-build-release-aar')
    project(':SDK:uniapp-v8-release').projectDir = file('./SDK/LocalAAR/uniapp-aars/uniapp-v8-release-aar')
    project(':SDK:uniMPSDK-V2-release').projectDir = file('./SDK/LocalAAR/uniapp-aars/uniMPSDK-V2-release-aar')
}