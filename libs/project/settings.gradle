apply from: '../../commonGradle/gradle_config_optimize.gradle'

dependencyResolutionManagement {
    repositories {
        maven {
            name = "GitHubPackages"
            url 'https://maven.pkg.github.com/Heart-Beats/BaseProject'
            credentials {
                username = 'Heart-Beats'
                password = GITHUB_PACKAGES_READ_TOKEN
            }
        }
    }

    versionCatalogs {
        // 默认创建的 libs 进行配置
        libs {
            from("com.hl:catalog:1.0.2")

            version("androidGradlePlugin", "8.0.1")
            version("kotlin", "1.8.20")
        }
    }
}

rootProject.name='Project'

// 不添加 includeBuild， 该项目下无法依赖 uikit 项目下的相关库
includeBuild('../uikit')

includeBase()
includeSDK()
include ':utils'

def includeBase() {
    include ':base-api', ':base-app-res', ':base-ui', ':base-arch',':base-rv-adapter'

    project(':base-api').projectDir = file('./base/api')
    project(':base-app-res').projectDir = file('./base/app-res')
    project(':base-ui').projectDir = file('./base/ui')
    project(':base-arch').projectDir = file('./base/arch')
    project(':base-arch').projectDir = file('./base/arch')
    project(':base-rv-adapter').projectDir = file('./base/rv-adapter')
}

def includeSDK() {
    include ':SDK:Umeng', ':SDK:Pay', ':SDK:TencentCloud', 'SDK:Navigation', ':SDK:Web', ':SDK:ActivityResult',
            ':SDK:XLogInit', ':SDK:MMKVSharedPreferences', ':SDK:JsonUtil', ':SDK:Permission', ':SDK:PictureSelector',
            ':SDK:ImageLoad', ':SDK:DateUtil', ':SDK:BitmapUtil', ':SDK:MimeType', ':SDK:Download', ':SDK:QRCode',
            ':SDK:Popup', ':SDK:SmsUtil', ':SDK:PreviewFile', ':SDK:Share', ':SDK:VideoPlayer', ':SDK:Camera',
            ':SDK:Update', ':SDK:Banner', ':SDK:ViewBinding'

    includeShadow()

    includeUniApp()
}

def includeShadow() {
    //    include ':SDK:shadow-plugin-manager'
    include ':SDK:shadow-lib', ':SDK:shadow-init'

    //    project(':SDK:shadow-plugin-manager').projectDir = file('./SDK/Shadow/plugin-manager')
    project(':SDK:shadow-lib').projectDir = file('./SDK/Shadow/shadow-lib')
    project(':SDK:shadow-init').projectDir = file('./SDK/Shadow/shadow-init')
}

def includeUniApp() {
    include ':SDK:UniMP'

    // 加入 UniApp 需要依赖的本地 AAR
    includeUniAppAARS()
}

def includeUniAppAARS() {
    include ':SDK:uniapp-base-oaid-sdk', ':SDK:uniapp-breakpad-build-release', ':SDK:uniapp-v8-release', ':SDK:uniMPSDK-V2-release'

    project(':SDK:uniapp-base-oaid-sdk').projectDir = file('./SDK/LocalAAR/uniapp-aars/base-oaid-sdk-aar')
    project(':SDK:uniapp-breakpad-build-release').projectDir = file('./SDK/LocalAAR/uniapp-aars/breakpad-build-release-aar')
    project(':SDK:uniapp-v8-release').projectDir = file('./SDK/LocalAAR/uniapp-aars/uniapp-v8-release-aar')
    project(':SDK:uniMPSDK-V2-release').projectDir = file('./SDK/LocalAAR/uniapp-aars/uniMPSDK-V2-release-aar')
}