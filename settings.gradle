import java.util.regex.Matcher
import java.util.regex.Pattern

apply from: './commonGradle/gradle_config_optimize.gradle'

dependencyResolutionManagement {
    repositories {
        maven {
            name = "GitHubPackages"
            url 'https://maven.pkg.github.com/Heart-Beats/BaseProject'
            credentials {
                username = 'Heart-Beats'
                password = GITHUB_PACKAGES_READ_TOKEN
            }
        }
    }

    versionCatalogs {
        // 默认创建的 libs 进行配置
        libs {
            from("com.hl:catalog:1.0.2")

            version("androidGradlePlugin", "8.0.1")
            version("kotlin", "1.8.20")
        }
    }
}

rootProject.name = "BaseProject"

include ':app', ':benchmark'

def includeBuildProjects = ['libs/project', 'libs/uikit']


// 是否使用远程依赖库方式
if (!isUseRemoteDependencies.toBoolean()) {
    includeBuildProjects.forEach {
        println "开始添加构建 -------------> $it"
        includeBuild(it)
    }
}

//setting.gradle 加载和评估配置阶段完成
gradle.settingsEvaluated {
    println '设置加载完毕'
}

//项目加载完成
gradle.projectsLoaded {
    println "${it} ---------> projectsLoaded"

    gradle.rootProject.buildscript {
        println '开始执行初始化配置脚本'
    }
}

def getCurrentFlavor(Project project) {
    Gradle gradle = project.getGradle()
    String tskReqStr = gradle.getStartParameter().getTaskRequests().toString()
    Pattern pattern = Pattern.compile("(assemble|generate|bundle)(.*?)(Release|Debug)")
    Matcher matcher = pattern.matcher(tskReqStr)
    String channel = ""
    if (matcher.find()) {
        channel = matcher.group(2).toLowerCase()
    }
    return channel
}

gradle.addProjectEvaluationListener(new ProjectEvaluationListener() {
    void beforeEvaluate(Project project) {
        println "${project} -------------> beforeEvaluate"
        println "currentFlavor -------------> ${getCurrentFlavor(project)}"
    }

    void afterEvaluate(Project project, ProjectState state) {
        println "${project} -------------> afterEvaluate"
        println "currentFlavor -------------> ${getCurrentFlavor(project)}"
    }
})